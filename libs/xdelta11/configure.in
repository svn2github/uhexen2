dnl -*- Mode: m4 -*-
dnl Process this file with autoconf to produce a configure script.
AC_REVISION([configure.in,v 1.2 1998/10/03 00:17:50 jmacd Exp])dnl
AC_INIT(xdelta.c)
AM_CONFIG_HEADER(config.h)

dnl $Format: "AM_INIT_AUTOMAKE(xdelta, $ReleaseVersion$, no-define)" $
AM_INIT_AUTOMAKE(xdelta, 1.1.4, no-define)

AM_MAINTAINER_MODE

DEBUGFLAG=
PROFILEFLAG=

AC_ARG_ENABLE(debug, [  --enable-debug          turn on debugging [default=no]])
AC_ARG_ENABLE(profile, [  --enable-profile        turn on profiling [default=no]])
AC_ARG_ENABLE(glib2, [  --disable-glib2         use glib-1.2, not 2.0 [default=no]])

TMPCFLAGS="$CFLAGS"
CFLAGS=

AC_PROG_CC
AC_PROG_CPP

CFLAGS=$TMPCFLAGS

changequote(,)dnl
if test "x$GCC" = "xyes"; then
  case " $CFLAGS " in
  *[\ \	]-Wall[\ \	]*) ;;
  *) CFLAGS="$CFLAGS -Wall" ;;
  esac

  if test "x$enable_debug" = "xyes"; then
    DEBUGFLAG="-g"
  fi

  if test "x$enable_profile" = "xyes"; then
    PROFILEFLAG="-pg"
  fi

  if test -n "$DEBUGFLAG"; then
    case " $CFLAGS " in
    *[\ \	]$DEBUGFLAG[\ \	]*) ;;
    *) CFLAGS="$DEBUGFLAG $CFLAGS" ;;
    esac
  else
    case " $CFLAGS " in
    *[\ \	]-O[0-9\ \	]*) ;;
    *) CFLAGS="$CFLAGS -O3" ;;
    esac
  fi

  if test -n "$PROFILEFLAG"; then
    case " $CFLAGS " in
    *[\ \	]$PROFILEFLAG[\ \	]*) ;;
    *) CFLAGS="$PROFILEFLAG $CFLAGS" ;;
    esac
  fi
fi
changequote([,])dnl

AM_PROG_LIBTOOL
AC_HEADER_STDC
AC_PROG_MAKE_SET

AC_CHECK_FUNCS(gettimeofday)
AC_HEADER_TIME

AC_PATH_PROGS(EMACS, emacs xemacs, emacs)

top_srcdir_absolute=`cd $srcdir; pwd`
AC_SUBST(top_srcdir_absolute)

XD_EXTRA_FLAGS=
AC_SUBST(XD_EXTRA_FLAGS)

if test "x$enable_glib2" = "xno"; then
XD_EXTRA_FLAGS=-D_XD_USE_GLIB1
AC_SUBST(XD_EXTRA_FLAGS)
AC_DEFINE(_XD_USE_GLIB1, 1, Use glib-1.2.x not glib-2.x)
AM_PATH_GLIB(1.2.8,,
  AC_MSG_ERROR(Test for GLIB failed.))
else
AM_PATH_GLIB_2_0(2.0.0,,
  AC_MSG_ERROR(Test for GLIB failed.))
fi

AC_CHECK_LIB(z, gzsetparams, true,
  AC_MSG_ERROR(ZLIB library not found))
AC_CHECK_HEADER(zlib.h, true,
  AC_MSG_ERROR(ZLIB headers not found))

AC_OUTPUT(Makefile
	  doc/Makefile
          test/Makefile
          djgpp/Makefile
	  xdelta-config
	  libedsio/Makefile
	  libedsio/edsio-comp,

	  chmod +x libedsio/edsio-comp xdelta-config
	  )
